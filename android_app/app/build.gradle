apply plugin: 'com.android.application'

apply plugin: 'com.google.gms.google-services'

def admobProperties
if (rootProject.file("admob.properties").exists()) {
    admobProperties = new Properties()
    admobProperties.load(new FileInputStream(rootProject.file("admob.properties")))
} else {
    // test ids
    admobProperties = ['admob.application.id':'ca-app-pub-3940256099942544~3347511713',
    'admob.interstitiel.id': 'ca-app-pub-3940256099942544/1033173712']
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.tambapps.p2p.peer_transfer.android"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 20
        versionName "2.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [admobApplicationId: admobProperties['admob.application.id']]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ACTION_FINISHED_INTERSTITIEL_ID", '"' + admobProperties['admob.interstitiel.id'] + '"'
        }

        debug {
            // test interstitiel id
            buildConfigField "String", "ACTION_FINISHED_INTERSTITIEL_ID", '"ca-app-pub-3940256099942544/1033173712"'
        }
    }
    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }
    buildToolsVersion = '29.0.3'
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/README.md'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Crashlytics Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.android.gms:play-services-ads:20.1.0'
    // for fandem
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'me.relex:circleindicator:2.1.6'
}

apply plugin: 'com.google.firebase.crashlytics'